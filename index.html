<!DOCTYPE html>
<html>
<head>
	<title>Peppa Pig</title>
</head>
<style type="text/css">

	html, body {
		width: 100%;
		height: 100%;
	}
	* {
		margin: 0;
		padding: 0;
	}

</style>

<body onload="main()">
	<canvas id="webgl" width="600" height="400"></canvas>
</body>

<script type="x-webgl/x-vertex-shader" id="vertexShader">
	attribute vec2 aVertexPosition;
	varying vec2 v_texCoord;
	void main() {
		v_texCoord = aVertexPosition;
		gl_Position = vec4(aVertexPosition, 0.0, 1.0);
	}
</script>

<script type="x-webgl/x-fragment-shader" id="fragmentShader">
	#ifdef GL_ES
	precision mediump float;
	#endif

	// glslsandbox uniforms
	uniform float time;
	uniform vec2 resolution;

	// shadertoy globals
	float iTime;
	vec3  iResolution;


	#define PI 3.14159


	float hash(float f)
	{
	    return fract( sin(f * 77321.81283) );
	}

	float sub( float d, float d2 )
	{
	    return max(-d2, d);
	}

	float add( float d2, float d )
	{
	    return min(d2, d);
	}

	float sect( float d, float d2 )
	{
	    return max(d,d2);
	}

	vec2 rotate( float ang, vec2 v )
	{
	    float s = sin(ang);
	    float c = cos(ang);
	    return vec2( c*v.x-s*v.y, s*v.x+c*v.y );
	}

	float circle( vec2 where, vec2 uv, float radius )
	{
	    vec2 dv = uv-where;
	    return sqrt(dot(dv,dv))-radius;
	}

	float ellips( vec2 where, vec2 uv, vec2 ab, float radius )
	{
	    vec2 dv = (uv-where) / ab;
	    return length(dv)-radius;
	}
	float halfellips( vec2 where, vec2 uv, vec2 ab, float radius )
	{
	    float l = uv.y > where.y ? length((uv-where) / ab) : 1.;
	    return l-radius;
	}

	float square( vec2 where, vec2 uv, vec2 hsize )
	{
	    vec2 dv = abs(uv - where) - hsize;
	    return min(0., max(dv.x, dv.y)) + length(max(dv,0.));
	}

	float rsquare( vec2 where, vec2 uv, vec2 hsize, float edge )
	{
	    float d =square(where,uv,hsize);
	    return d-edge;
	}


	float blownQuad( vec2 uv, vec2 size, float roundness, float edge )
	{
	    float d=0.;
	    d=ellips( vec2(0.), uv, vec2(roundness,1.), size.y );
	    d=sect(d,ellips(vec2(0.),uv,vec2(1.,roundness),size.x));
	    return d;
	}

	float hline( vec2 uv, float len )
	{
	    vec2 cp = uv;
	    cp.y=0.;
	    cp.x = max(min(uv.x,len*.5), -len*.5);
	    return distance(cp,uv);
	}

	float vline( vec2 uv, float len )
	{
	    return hline(rotate(PI*.5, uv),len);
	}

	float line( vec2 uv, vec2 a, vec2 b )
	{
	    vec2 d=a-b;
	    vec2 m=(a+b)*.5;
	    return hline( rotate(-atan(d.y,d.x), uv-m), length(d) );
	}
	float dLine(vec2 p, vec2 a, vec2 b) 
	{
	    b -= a;  
	    p -= a;
	    return length (p - clamp (dot (p, b) / dot (b, b), .0, 1.) * b);
	}
	vec2 bzPos(float t, vec2 a, vec2 b, vec2 c) 
	{
	    float mT = 1.-t;
	//    vec2 pos = a * 1.      * mT*mT +
	//               b * 2.* t   * mT    + 
	//               c * 1.* t*t          ;
	    return a *           mT*mT +
	           b * 2.* t   * mT    + 
	           c *     t*t          ;
	}
	float dBezierSegs(vec2 p, vec4 ac, vec2 b, float dt) {
	    float d = 9999.;
	    vec2 a = ac.xy, c = ac.zw, q = a, r;
	    
	    for(float t=0.; t<=1.; t+=.03) {
	        r = bzPos(t,a,b,c);
	        d = min(d, dLine(p,q,r));
	        q = r;
	        if(t>dt) return d;
	    }
	    
	    return d;
	}


	float trapezoid( vec2 uv, vec3 s, float edge ) // top,bottom,height
	{
	    vec2 cp0 = vec2(-s.x,s.z)*.5;
	    vec2 cp1 = cp0+vec2(s.x,0.);
	    vec2 cp2 = vec2(-s.y,-s.z)*.5;
	    vec2 cp3 = cp2+vec2(s.y,0.);
	    float d = line( uv, cp0, cp1 );
	    d = add( d, line( uv, cp1, cp3 ) );
	    d = add( d, line( uv, cp3, cp2 ) );
	    d = add( d, line( uv, cp2, cp0 ) );
	    return d-edge;
	}

	float head( vec2 uv )
	{
	    float d = 0.;
	    d = ellips( vec2(0., 0.2), rotate(-.2, uv), vec2(1.1, 1.), .15 );
	    d = add( d, trapezoid(rotate(-.4, uv  + vec2(.01, -.27)), vec3(.06, .3, .03), .06));
	    d = add( d, trapezoid(rotate(-2., uv  + vec2(.14, -.31)), vec3(.15, .15, .01), .02));
	    d = add( d, trapezoid(rotate(-1.9, uv  + vec2(.07, -.34)), vec3(.15, .15, .01), .02));
	    return d;
	}
	float face( vec2 uv )
	{
	    float d=0.;
	    d = ellips( vec2(-.02, .19), rotate(-0.6,uv), vec2(1.,1.2), .04);
	    return d;
	}

	float eyes(vec2 uv)
	{
	float d = 0.;
	    d = ellips( vec2(.08, .27), rotate(-0.6,uv), vec2(1.,1.), .02);
	    d = add(d, ellips( vec2(.16, .26), rotate(-0.6,uv), vec2(1.,1.), .016));
	    return d;
	}
	float dott(vec2 uv)
	{
	float d = 0.;
	    d = ellips( vec2(.075, .27), rotate(-0.6,uv), vec2(1.,1.), .005);
	    d = add(d, ellips( vec2(.157, .26), rotate(-0.6,uv), vec2(1.,1.), .003));
	    d = add(d, ellips( vec2(.294, .185), rotate(-0.6,uv), vec2(1.,1.), .005));
	    d = add(d, ellips( vec2(.32, .175), rotate(-0.6,uv), vec2(1.,1.), .005));
	    return d;
	}
	float mouse(vec2 uv)
	{
	float d = 0.;
	    d = dBezierSegs(uv, vec4(-0.04, 0.15, 0.0, 1.5), vec2(0.2, -0.05), 0.3);
	    return d;
	}
	float nose(vec2 uv)
	{
	        float d = 0.;
	    d = ellips( vec2(.24, .26), rotate(-0.3,uv), vec2(1.,1.3), .04);
	    return d;
	}
	float body(vec2 uv)
	{
	        float d = 0.;
	    d = halfellips( vec2(-.04, -.2), rotate(.06,uv), vec2(1.,1.8), .2);
	    return d;
	}
	float feet(vec2 uv)
	{
	    float d = 0.;
	    d = line(uv, vec2(-0.2, 0.03), vec2(-0.31, -0.02));
	    d = add(d, line(uv, vec2(0.12, 0.03), vec2(0.24, 0.)));
	    d = add(d, line(uv, vec2(0.02, -0.203), vec2(0.02, -0.28)));
	    d = add(d, line(uv, vec2(-0.13, -0.194), vec2(-0.13, -0.27)));
	    d = add(d, line(uv, vec2(-0.25, -0.18), vec2(0.15, -0.21)));
	    d = add(d, dBezierSegs(uv, vec4(-0.31, .0, -0.3, -0.04), vec2(-0.25, .0), 6.5)); // 爪子
	    d = add(d, dBezierSegs(uv, vec4(0.24, .02, 0.24, -0.02), vec2(0.16, .01), 6.5)); // 爪子
	    d = add(d, dBezierSegs(uv, vec4(-0.24, -.11, -.26, -.08), vec2(-0.32, -.09), 6.5));
	    d = add(d, trapezoid(rotate(-1.5, uv  + vec2(0.12, .28)), vec3(.008, .015, .08), .005));
	    d = add(d, trapezoid(rotate(-1.5, uv  + vec2(-0.03, .28)), vec3(.008, .015, .08), .005));
	    return d;
	}


	float nsmoothstep(float e0, float e1, float x, float f)
	{
	    f=1./f;
	    x = smoothstep(e0*f,e1*f,x);
	    return x;
	}


	vec3 strokestep( float e, float m, float x, vec3 left, vec3 mid, vec3 right )
	{
	    return
	    mix( mix( left, mid, smoothstep( -e-m, -m, x ) ), right, 
	        smoothstep( m, e+m, x ) );
	}


	void mainImage( out vec4 fragColor, in vec2 fragCoord )
	{
	    // Normalized pixel coordinates (from 0 to 1)
	    vec2 uv = fragCoord/iResolution.xy;
	    uv -= .5;
	    uv.x *= iResolution.x/iResolution.y;
	    
	    // shapes
	    vec2 ruv=rotate(sin(iTime / 0.5)*.03,uv);
	    float fd = head( ruv );
	    float fc = face( ruv );
	    float ee = eyes(ruv);
	    float dt = dott(ruv);
	    float ms = mouse(ruv);
	    float ns = nose(ruv);
	    float bd = body(uv);
	    float ft = feet(uv);

	    // materials

	    vec3 bgb   = vec3(.7, .5, 0.4) * pow( (1.3-length(uv)), .2 );
	    vec3 black = vec3(.3);
	            vec3 orang = vec3(1., .45, 0.2);
	    vec3 white = vec3(1.);

	    vec3 red = vec3(1.0,0.25,0.2);
	    vec3 pink = vec3(0.96, 0.76, 0.75);
	    
	    // anti-aliasing
	    const float AA  = 0.002;
	    const float M   = 0.004;
	    
	    vec3 col=vec3(0.);
	    float a=0.;
	        
	    col = strokestep(AA, M , bd, red, black, bgb);
	    col = strokestep(AA, M, fd, pink, black, col);
	    col = strokestep(AA, M, fc, orang, orang, col);
	    col = strokestep(AA, AA , ee, white, black, col);
	    col = strokestep(AA, AA , ns, pink, black, col);
	    col = strokestep(AA, M , dt, white, black, col);
	    col = strokestep(AA, M , ms, white, black, col);
	    col = strokestep(AA, M , ft, black, black, col);
	    

	    // face > head
	    a = nsmoothstep( 0., AA, fc, 1. );
	    col = mix( orang, col, a );
	    

	    // Output to screen
	    fragColor = vec4(col,1.0);
	}

	void main(void)
	{
	    iTime = time;
	    iResolution = vec3(resolution, 0.0);
	    mainImage(gl_FragColor, gl_FragCoord.xy);
	}

</script>
<script type="text/javascript">
	var vertexShader = document.getElementById("vertexShader").text,
		fragmentShader = document.getElementById("fragmentShader").text;
</script>

<script type="text/javascript">
	function main() {
		var drawing = document.getElementById("webgl"), gl;
		drawing.width = document.body.offsetWidth;
		drawing.height = document.body.offsetHeight;
		if(drawing.getContext) {
			try{
				gl = drawing.getContext("experimental-webgl", {alpha: true});
			} catch(ex) {
				// do nothing
			}
			if(gl) {
				// console.table([{no_error: gl.NO_ERROR, invalid_enum: gl.INVALID_ENUM, invalid_value: gl.INVALID_VALUE, invalid_operation: gl.INVALID_OPERATION, out_of_memory: gl.OUT_OF_MEMORY, context_lost_webgl: gl.CONTEXT_LOST_WEBGL}]);

				// use webgl
				gl.clearColor(1, 1, 1, 1);
				gl.clear(gl.COLOR_BUFFER_BIT);
				gl.viewport(0, 0, drawing.width, drawing.height);
				var buffer = gl.createBuffer();
				gl.bindBuffer(gl.ARRAY_BUFFER, buffer);
				gl.bufferData(gl.ARRAY_BUFFER, new Float32Array([0, 0.5, 1]), gl.STATIC_DRAW);

				var vertShader = gl.createShader(gl.VERTEX_SHADER);
				var fragShader = gl.createShader(gl.FRAGMENT_SHADER);
				var program = gl.createProgram();
				var parameters = { time: +new Date() / 1000, resolution: [drawing.width, drawing.height] };
				var flag = true;
				function render() {
					gl.shaderSource(vertShader, vertexShader);
					gl.compileShader(vertShader);
					gl.shaderSource(fragShader, fragmentShader);
					gl.compileShader(fragShader);
					if(flag) {
						flag = false;
						gl.attachShader(program, vertShader);
						gl.attachShader(program, fragShader);
					}
					gl.linkProgram(program);
					gl.useProgram(program);
					var loc_time = gl.getUniformLocation(program, "time");
					var loc_resolution = gl.getUniformLocation(program, "resolution");
					gl.uniform1f(loc_time, +new Date() / 1000 - parameters.time);
					gl.uniform2f(loc_resolution, parameters.resolution[0], parameters.resolution[1])

					var aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
					// console.log("aVertexPosition", aVertexPosition);
					gl.enableVertexAttribArray(aVertexPosition);
					// if(!gl.getShaderParameter(vertShader, gl.COMPILE_STATUS)) {
					// 	console.log("compile error\n", gl.getShaderInfoLog(vertShader));
					// }
					// if(!gl.getShaderParameter(fragShader, gl.COMPILE_STATUS)) {
					// 	console.log("compile error\n", gl.getShaderInfoLog(fragShader));
					// }
					// if(!gl.getProgramParameter(program, gl.LINK_STATUS)) {
					// 	console.log("link error\n", gl.getProgramInfoLog(program));
					// }
					// var errorCode = gl.getError();
					// while(errorCode) {
					// 	console.log("Error occurred: " + errorCode);
					// 	errorCode = gl.getError();
					// }

					var vertices = new Float32Array([-1, -1, 1, -1, 1, 1, -1, -1, 1, 1, -1, 1]),
						buffer = gl.createBuffer(),
						vertexSetSize = 2,
						vertexSetCount = vertices.length / vertexSetSize;
					gl.bindBuffer(gl.ARRAY_BUFFER, buffer);
					gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);
					gl.vertexAttribPointer(aVertexPosition, vertexSetSize, gl.FLOAT, false, 0, 0);
					gl.drawArrays(gl.TRIANGLES, 0, vertexSetCount);

					requestAnimationFrame(render);
				}
				render();

			} else {
				console.log("webgl not supported");
			}
		}
	}
</script>
</html>